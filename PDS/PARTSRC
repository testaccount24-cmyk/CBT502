*   THIS IS THE RELEVANT PIECE OF MVS 3.8 AMASPZAP SOURCE CODE
*******************
*           THIS ROUTINE SIMULATES BLDL. AND STOW USING, EXCP
*              TO READ THE DIRECTORY AND WRITE THE DIRECTORY
*******************
         SPACE 1
*      THIS CODE SETS UP THE CCW'S FOR THE READING OF THE DIRECTORY
DIRREAD  MVC   IOCCW3(4),IOCCW6         SET UP TIC TO RD C.P.
         MVC   IOCCW7+1(3),RDAREA+1     SET INPUT BUFFER
         XC    SCHARG(8),SCHARG         ZERO OUT M AND R
         L     WK7,LIB+44          GET DEB ADDRESS
         MVC   SCHARG+1(6),STEXT-2(WK7)  SET SEEK ADDRESS
         MVC   SCHARG+1(2),LIB2+6  SET BB IN SRCH MBBCCHHR
UPDATEIO LA    1,DECB4+4                IOB ADDR
         SVC   0                        EXCP
         WAIT  1,ECB=DECB4              WAIT ON I/O
         BC    15,ERRTST
         SPACE 1
*        THIS CODE SET UP THE CCW'S FOR THE DIRECTORY UPDATE
DIRWRITE MVC   IOCCW3(8),IOCCW3B        SET UP UPDATE CCW
         MVC   IOCCW3+1(3),RDAREA+1     SET OUTPUT AREA ADDR
         MVC   SCHARG+3(5),TBUF+1       SET SEARCH ARG
         BC    15,UPDATEIO              TO UPDATE DIRECTORY
         SPACE 1
OPENLIB  TM    LIB+OPENBIT,OPENED       HAS IT BEEN OPENED YET   S21995
         BCR   1,LNKRG1                 YES,LNKRG1  RETURN       S21995
         RDJFCB LIB                                              S21995
         LA    RG5,JFCBAREA       GET ADDRESS OF JFCB            S21995
         CLC   0(44,RG5),DSCBDS   JFCB FOR FORMAT4?              S21995
         BC    7,RSRV              NO, ISSUE RESERVE AND OPEN    S21995
ASKOP    ST    LNKRG1,LNKSAVE2         SAVE LINK REGISTER        A38645
         BAL   LNKRG1,UPVTRTN          REQUEST TO UPDATE VTOC    A38645
         L     LNKRG1,LNKSAVE2         RESTORE LINK REGISTER     A38645
         OI    CDBUF,X'40'         INSURE A CAPITAL LETTER       S21995
         MVC   JFCBAREA(12),F4DSCB    MOVE FORMAT4.DSCB INTO DSN S21995
         MVC   JFCBAREA+12(32),BLANKS   PAD REMAINDER OF DSNAME  S21995
         CLI   CDBUF,C'N'               OKAY TO MODIFY           A53326
         BC    8,NOF4MOD                NO-NOTIFY USER           A53326
         CLI   CDBUF,C'Y'          CHECK FOR Y                   A53326
         BC    7,ASKOP             NO, REISSUE MSG               A53326
RSRV     L     RG5,LIB2+44         GET DEB ADDR FOR SYSLIB       S21995
         LA    RG5,0(RG5)          CLEAR HI BYTE                 S21995
         L     RG5,32(RG5)         PUT UCB AD IN RG5             YM5147
         ST    RG5,UCBAD           PUT IN UCBAD FOR RESERVE      YM5147
         MVC   RNAME(6),28(RG5)    MOVE MINOR NAME FROM UCB      YM5147
         CLC   JFCBAREA(12),F4DSCB JFCB FOR FORMAT4?             YM5147
         BE    VTOCRSRV            RESERVE 'SYSVTOC'             YM5147
         RESERVE (MAJOR,JFCBAREA,E,44,SYSTEMS),RET=HAVE,UCB=UCBAD 21995
         B     RESERVED           BRANCH AROUND RESERVE FOR VTOC YM5147
VTOCRSRV RESERVE (QNAME,RNAME,E,6,SYSTEMS),RET=HAVE,UCB=UCBAD    YM5147
RESERVED EQU  *                                                  YM5147
         MVI   RSVSW,X'FF'         INDICATE RESERVE ISSUED       S21995
OPEN2    OPEN  (LIB,(UPDATE))           OPEN                     S21995
         BCR   15,LNKRG1                DONE                     S21995
         SPACE 1
NOF4MOD  OI    CTLBTE,NAMEERR                                    S21995
         LA    RG5,NOVTOC               ERROR MSG                S21995
         BAL   LNKRG1,WRITE0A            PRINT OUT ERROR MSG     S21995
OPENRT   MVC   RETCODE(1),RETCOD08     .INDIC SPZAP-INP ERROR. @ZA06023
         LM    0,15,XFERSAVE           RESTORE REGS              S21995
         BCR   15,LNKRG3               GET NEXT CARD             S21995
         SPACE 2
*
*
*   ----  LARGE SNIPPAGE  -----
*
*
***********************************************************************
*                                                                     *
*  THIS ROUTINE WRITES MESSAGE AMA117D TO OPERATOR TO REQUEST         *
*  PERMISSION TO UPDATE VTOC                                          *
*                                                                     *
***********************************************************************
UPVTRTN  ST    LNKRG1,LNKSAVE3         SAVE LINK REGISTER        A38645
         BALR  LNKRG1,0                ESTABLISH                 A38645
         USING *,LNKRG1                 ADDRESSABILITY           A38645
         EXTRACT ADDRSV,'S',FIELDS=(TIOT) GET ADDR ACT TASK      A38645
         L     RG5,ADDRSV              RG5 POINTS TO TIOT        A38645
         MVC   UPVTMSG+66(8),0(RG5)    MOVE JOBNAME TO MSG BUFFERA38645
         LA    RG5,24(RG5)             INCREMENT TO FIRST DD ENT A38645
CHKLIB   CLC   4(6,RG5),SYSLIB         SYSLIB DD                 A38645
         BC    8,LIBUCB                GET SYSLIB UCB ADDR       A38645
         XC    ADDRSV(4),ADDRSV        CLEAR ADDRESS AREA        A38645
         MVC   ADDRSV+3(1),0(RG5)      MOVE LEN OF DD ENTRY      A38645
         A     RG5,ADDRSV              RG5 POINTS TO NEXT DD ENT A38645
         BC    15,CHKLIB               CHECK IF ITS SYSLIB       A38645
LIBUCB   L     RG5,16(RG5)             REL ADDR OF SYSLIBS UCB   A38645
         DEVTYPE SYSLIB,DEVBUF         GET SYSLIB DEVICE TYPE    A38645
         CLI   DEVBUF+3,X'05'          IS IT A 2321 DATA CELL    A38645
         BC    7,NOT2321               NOT 2321                  A38645
*  DEVICE IS A 2321 DATA CELL                                         *
         MVC   UPVTMSG+53(6),4(RG5)    2321 VOLSER TO MSG BUFFER A38645
         MVC   UPVTMSG+64(1),1(RG5)    BIN NUMBER TO MSG BUFFER  A38645
         OI    UPVTMSG+64,X'F0'        CONVERT TO EBCDIC         A38645
         MVI   UPVTMSG+63,C'/'         INSERT DELIMETER          A38645
*  FIND BEGINNING OF 2321 UCB FOR UNIT ADDRESS FIELD                  *
         ST    RG5,ADDRSV              SAVE ADDR OF CELL DESCR   A38645
         LH    RG5,0(RG5)              LOAD BIN NUMBER           A38645
         SLA   RG5,4                   TIMES BYTES/CELL ENTRY    A38645
         LA    RG5,56(RG5)             PLUS DISP TO BIN0 DESCR   A38645
         LCR   RG5,RG5                                           A38645
         A     RG5,ADDRSV              RG5 POINTS TO BEGIN OF UCBA38645
         BC    15,UNITNAME             MOVE UNIT NAME            A38645
NOT2321  MVC   UPVTMSG+53(6),28(RG5)   NON 2321 VOLSER TO MSG BUFA38645
UNITNAME MVC   UPVTMSG+60(3),13(RG5)   MOVE UNIT NAME TO MSG BUF A38645
         XC    WTOECB(4),WTOECB        CLEAR ECB                 A38645
UPVTMSG  WTOR  'AMA117D REPLY Y OR N TO UPDATE VTOC                    X
                  ',CDBUF,1,WTOECB,ROUTCDE=1,DESC=2
         WAIT  ECB=WTOECB                                        A38645
         MVC   UPVTMSG+53(21),UPVTMSG+52 BLANK DYNAM AREA        A38645
         L     LNKRG1,LNKSAVE3         RESTORE LINK REGISTER     A38645
         BR    LNKRG1                  .RETURN TO CALLER.      @ZA17658
         EJECT                                                 @ZA17658
